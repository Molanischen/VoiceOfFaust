//-----------------------------------------------
// Karplus-Strong effect
//-----------------------------------------------

KPcenters(freq,oct) = VocoderFreqs(KPbottom,KPtop):(par(i,nrBands, _,freq * oct:*:min(SR/2):dezip));

KPanalizer(audio,freq,fidelity) =
    analizer(audio,freq,fidelity,enableDeEsser)
    :par(i,nrBands,((_*KPvocoderStrength,((KPvocoderStrength*-1)+1)):+
    :linear2db) );//adapt to eq instead of bandpass


//serial equaliser, with parameters exposed:
eqOfN(1) = bus(4)<:(selector(1,4),selector(2,4),selector(3,4),selector(0,4)):peak_eq_cq;
eqOfN(n) = (eqOfN(n-1),_,_,_):eqOfN(1);

EQbank(Oscilator,q) =
    ((bus(nrBands)<:(bus(nrBands),OffSet)<:(par(i, nrBands, (selector(i,nrBands+1)+selector(nrBands,nrBands+1))))),bus(nrBands))
    :(bus(2*nrBands),(q<:bus(nrBands)))
    :((Oscilator,interleave(nrBands,3)):eqOfN(nrBands))
    with {
        N = 3; //uneven only: 1,3,5
        AvgVolume = sumOfN(nrBands)/nrBands;
        MinVolume = minOfN(nrBands);
        MaxVolume = maxOfN(nrBands);
        OffSet    =
           bus(nrBands)<:bus(nrBands*4):
           (KPeqCutBoost>0),
           (((KPeqCutBoost+1)*-AvgVolume)+(KPeqCutBoost*MaxVolume)),
           ((((KPeqCutBoost*-1)+1)*-AvgVolume)+KPeqCutBoost*-MinVolume):
           select2;
        };
KPvocoder(audio,feedback,freq,fidelity)= (feedback,KPvocoderQ,KPanalizer(voice(audio,freq),freq,fidelity),KPcenters(freq,1)):EQbank;

stringloopFBpath(freq, oct,feedback,phase,nonLinearity,frequencyMod,DC) =
      //(( fdelay4(Pmax, P-2)))
  (( NLFM : fdelay4(Pmax, P-2) : loopfilter))
    with {
      P = SR/(freq*oct) ; // fundamental period in samples
      Pmax = 4096; // maximum P (for delay-line allocation)

      rho = pow(0.001,1.0/(freq*oct*feedback*8)); // multiplies loop-gain

      // Original EKS damping filter:
      b1 = 0.5*bright; b0 = 1.0-b1; // S and 1-S
      dampingfilter1(x) = rho * ((b0 * x) + (b1 * x'));
      // Linear phase FIR3 damping filter:
      h0 = (1.0 + bright)/2; h1 = (1.0 - bright)/4;
      dampingfilter2(x) = rho * (h0 * x' + h1*(x+x''));

      loopfilter = _*rho;//:dampingfilter1; // or dampingfilter2

      //nonlinear allpass filter (nonLinearModulator is declared in instrument.lib)
      NLFM =  MyNonLinearModulator(nonLinearity,frequencyMod*freq*oct,phase,DC);
    };

feedbackADSR(audio,freq) = tanh(voice(audio,freq)*2:amp_follower(KPrelease)):pow(4);

stringloopBank(freq,fidelity,audio,index,feedback,phaseLL,phaseL,phase,phaseH,phaseHH,DCnonlinLL,DCnonlinL,DCnonlin,DCnonlinH,DCnonlinHH) =
    (_+feedback
    <:(    stringloopFBpath(freq,0.25,feedbackLL*feedbackADSR(audio,freq),phaseLL,nonLinLL,frequencyModLL,DCnonlinLL),
    stringloopFBpath(freq,0.5,feedbackL*feedbackADSR(audio,freq),phaseL,nonLinL,frequencyModL,DCnonlinL),
    stringloopFBpath(freq,1,feedbackM*feedbackADSR(audio,freq),phase,nonLin,frequencyMod,DCnonlin),
    stringloopFBpath(freq,2,feedbackH*feedbackADSR(audio,freq),phaseH,nonLinH,frequencyModH,DCnonlinH),
    stringloopFBpath(freq,4,feedbackHH*feedbackADSR(audio,freq),phaseHH,nonLinHH,frequencyModHH,DCnonlinHH)
    )
    :> KPvocoder(audio,_,freq,fidelity))~ //no damping needed
    // :> KPvocoder(audio,_,freq,fidelity):dampingfilter1)~ //or dampingfilter2
//  feedback path
    ( compressor_mono(100,KPtresh,0,(1/(freq * 0.25 ))))
    :_*KPvolume
    with {

    // Original EKS damping filter:
    b1 = 0.5*bright; b0 = 1.0-b1; // S and 1-S
    dampingfilter1(x) = ((b0 * x) + (b1 * x'));
    // Linear phase FIR3 damping filter:
    h0 = (1.0 + bright)/2; h1 = (1.0 - bright)/4;
    dampingfilter2(x) = (h0 * x' + h1*(x+x''));
    };

stringloop(freq,audio,index,feedbackAmount,feedback,phase,DCnonlin) =
    ((_+feedback
    :
    stringloopFBpath(freq,1,feedbackAmount*feedbackADSR(audio,freq),phase,nonLin,frequencyMod,DCnonlin)
    )~ //or dampingfilter2
      //:KPvocoder(audio,_,freq):dampingfilter1)~ //or dampingfilter2
           ( compressor_mono(100,KPtresh,0,1/(freq *0.5 ))))
    *KPvolume
    with {

    // Original EKS damping filter:
    b1 = 0.5*bright; b0 = 1.0-b1; // S and 1-S
    dampingfilter1(x) = ((b0 * x) + (b1 * x'));
    // Linear phase FIR3 damping filter:
    h0 = (1.0 + bright)/2; h1 = (1.0 - bright)/4;
    dampingfilter2(x) = (h0 * x' + h1*(x+x''));
    };


    MyNonLinearModulator(nonlinearity,freq,phase,DC) =
      //theta is modulated by a sine wave
      allpassnn(nlfOrder,(par(i,nlfOrder,tosc)))
      with{
        //theta is modulated by a sine wave generator
        tosc = (nonlinearity*PI*PHosci(freq,phase))+DC;

        //incoming signal is sent to the nonlinear passive allpass ladder filter
        //nonLinearFilterOsc = _ <: allpassnn(nlfOrder,(par(i,nlfOrder,tosc)));
      };
