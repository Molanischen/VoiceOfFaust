//-----------------------------------------------
// input->FM synth
//-----------------------------------------------
FM(audio,freq) = os.osc(freq/2+audio* 5000)*subLevel(audio,freq);

//co.compressor_mono(ratio,thresh,att,rel,x)
extremeLimiter = co.compressor_mono(100,-54,0.0008,0.002)*70;

equalpower = _<:			//3dB equal power curve
    log(_	 * 5.63875+1)/ 1.89292,
    log((1-_)* 5.63875+1)/ 1.89292;

//crossfades from no dynamics to normal dynamics to reversed ones
dynamics(limited, unlimited,dyn) =
    ((dyn*2:min(1):equalpower),
    (dyn-0.5:max(0)*2:equalpower:(_,_*-1):ro.cross(2)))
    :ro.interleave(2,2):((_,_):>_),((_,_):>_)
    :_*limited,_*unlimited:>_;

FMvoc(limited, unlimited,freq,vol,index,dyn) = os.osc((dynamics(limited, unlimited,dyn)*index)+freq)*vol;

FMSynth(limited, unlimited,freq,gain) =
    (
    FMvoc(limited, unlimited,freq*0.25,gain*FMvolLL,FMindexLL,FMdynLL),
    FMvoc(limited, unlimited,freq*0.5,gain*FMvolL,FMindexL,FMdynL),
    FMvoc(limited, unlimited,freq,gain*FMvol,FMindex,FMdyn),
    FMvoc(limited, unlimited,freq*2,gain*FMvolH,FMindexH,FMdynH),
    FMvoc(limited, unlimited,freq*4,gain*FMvolHH,FMindexHH,FMdynHH)
    ):>_
    ;

stereoFMSynth(limited, unlimited,freq,gain) = FMSynth(limited, unlimited,freq,gain*0.25) ,FMSynth(limited*-1, unlimited*-1,freq,gain*0.25);
